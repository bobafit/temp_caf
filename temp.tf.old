# Get the current client configuration from the AzureRM provider.
# This is used to populate the root_parent_id variable with the
# current Tenant ID used as the ID for the "Tenant Root Group"
# Management Group.

data "azurerm_client_config" "core" {}

# Declare the Azure landing zones Terraform module
# and provide a base configuration.

data "azurerm_subscriptions" "lists" {
  for_each              = toset(flatten([for k in local.cpe_cmdb : k]))
  display_name_contains = each.value
}

locals {
  sub_ids = { for subname, details in data.azurerm_subscriptions.lists : subname => join("", [for sub in details.subscriptions : sub.subscription_id if sub.display_name == subname]) }
  lists_of_ids = { for cat, subs in local.cpe_cmdb : cat => [for subname in subs : local.sub_ids[subname]] }
  platform = "platform"
}

module "enterprise_scale" {
  source  = "Azure/caf-enterprise-scale/azurerm"
  version = "6.2.0" # change this to your desired version, https://www.terraform.io/language/expressions/version-constraints

  default_location = var.location

  providers = {
    azurerm              = azurerm
    azurerm.connectivity = azurerm
    azurerm.management   = azurerm
  }

  root_parent_id = data.azurerm_client_config.core.tenant_id
  root_id        = var.root_id
  root_name      = var.root_name
  library_path   = "${path.root}/lib"

  deploy_core_landing_zones = false
  custom_landing_zones = {


    (var.root_id) = {
      display_name               = var.root_name
      parent_management_group_id = data.azurerm_client_config.core.tenant_id
      subscription_ids           = []
      archetype_config = {
        archetype_id   = "root_entity"
        parameters     = {}
        access_control = {}
      }
    }

    (local.platform) = {
      display_name               = "Platform"
      parent_management_group_id = var.root_id
      subscription_ids           = local.lists_of_ids.platform_subscriptions
      archetype_config = {
        archetype_id   = "platform"
        parameters     = {}
        access_control = {}
      }
    }

    "projects" = {
      display_name               = "Projects"
      parent_management_group_id = var.root_id
      subscription_ids           = local.lists_of_ids.projects_subscriptions
      archetype_config = {
        archetype_id   = "projects"
        parameters     = {}
        access_control = {}
      }
    }

    "prd" = {
      display_name               = "prd & stg env"
      parent_management_group_id = "projects"
      subscription_ids           = local.lists_of_ids.prd_subscriptions
      archetype_config = {
        archetype_id   = "prd"
        parameters     = {}
        access_control = {}
      }
    }

    "dev" = {
      display_name               = "Dev"
      parent_management_group_id = "projects"
      subscription_ids           = local.lists_of_ids.dev_subscriptions
      archetype_config = {
        archetype_id   = "dev"
        parameters     = {}
        access_control = {}
      }
    }

    "training" = {
      display_name               = "Training"
      parent_management_group_id = var.root_id
      subscription_ids           = local.lists_of_ids.training_subscriptions
      archetype_config = {
        archetype_id   = "training"
        parameters     = {}
        access_control = {}
      }
    }

    "decommissioned" = {
      display_name               = "Decommissioned"
      parent_management_group_id = var.root_id
      subscription_ids           = []
      archetype_config = {
        archetype_id   = "decommissioned"
        parameters     = {}
        access_control = {}
      }
    }

    "crowdstrike" = {
      display_name               = "Crowdstrike Deployment"
      parent_management_group_id = "projects"
      subscription_ids           = local.lists_of_ids.crowdstrike_subscriptions
      archetype_config = {
        archetype_id   = "crowdstrike"
        parameters     = {}
        access_control = {}
      }
    }

    "crowdstrike-platform" = {
      display_name               = "Crowdstrike Deployment - Platform"
      parent_management_group_id = "platform"
      subscription_ids           = local.lists_of_ids.crowdstrike_platform_subscriptions
      archetype_config = {
        archetype_id   = "crowdstrike-platform"
        parameters     = {}
        access_control = {}
      }
    }

  }
}